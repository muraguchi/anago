board <- {
	mappernum = 72,
	cpu_romsize = 0x20000, cpu_banksize = 0x4000,
	ppu_romsize = 0x20000, ppu_banksize = 0x2000,
	ppu_ramfind = false, vram_mirrorfind = true
};

/*

 Registers (**BUS CONFLICTS**):
 ---------------------------
 
   $8000-FFFF:  [PCRS DDDD]
     P = When a 1 is written after a 0 was previously written,
         the bottom three bits of the data bus are copied to the PRG bank select
     C = When a 1 is written after a 0 was previously written,
         the bottom four bits of the data bus are copied to the CHR bank select
     R = For games that have add-on sound, while 0,
         the ADPCM playback IC is held in reset and unable to make sound
     S = For games that have add-on sound, when the value written here changes
          (direction unknown because the datasheet contradicts itself), 
         the sound specified by the bottom 5 bits of the address bus is played.
         Leaving the value at 0 will probably result in erratic audio playback.
     D = the three- or four- bit bank number to switch to, as appropriate.
*/



function cpu_dump(d, pagesize, banksize)
{
        for(local i = 0; i < (pagesize-1); i++){
		  cpu_write(d,0x8000,          (i&0x7) );
		  cpu_write(d,0x8000,( (1<<7) |(i&0x7)));
		  cpu_read(d, 0x8000, 0x4000);
        }
	cpu_read(d, 0xC000, 0x4000);
}
function ppu_dump(d, pagesize, banksize)
{
	for(local i = 0; i < pagesize; i++){
		  cpu_write(d,0x8000,           (i&0xf)  );
		  cpu_write(d,0x8000,( (1<<6) | (i&0xf) ));
		  ppu_read(d, 0, banksize);
	}
}
